# == Class: consul-template
#
# Installs, configures, and manages consul-template
#
# === Parameters
#
# [*version*]
#   Specify version of consul-template binary to download.
#
# [*install_method*]
#   Defaults to `url` but can be `package` if you want to install via a system package.
#
# [*package_name*]
#   Only valid when the install_method == package. Defaults to `consul-template`.
#
# [*package_ensure*]
#   Only valid when the install_method == package. Defaults to `latest`.
#
# [*extra_options*]
#   Extra arguments to be passed to the consul-template agent
#
# [*init_style*]
#   What style of init system your system uses.
#
# [*purge_config_dir*]
#   Purge config files no longer generated by Puppet
class consul_template (
  $manage_user       = true,
  $user              = 'consul-t',
  $manage_group      = true,
  $purge_config_dir  = true,
  $group             = 'consul-t',
  $bin_dir           = '/usr/local/bin',
  $arch              = $consul_template::params::arch,
  $version           = $consul_template::params::version,
  $install_method    = $consul_template::params::install_method,
  $os                = $consul_template::params::os,
  $download_url      = "https://github.com/hashicorp/consul-template/releases/download/v${version}/consul-template_${version}_${os}_${arch}.tar.gz",
  $package_name      = $consul_template::params::package_name,
  $package_ensure    = $consul_template::params::package_ensure,
  $config_dir        = '/etc/consul-template',
  $extra_options     = '',
  $service_enable    = true,
  $service_ensure    = 'running',
  $consul_host       = 'localhost',
  $consul_port       = '8500',
  $consul_token      = '',
  $consul_retry      = '10s',
  $init_style        = $consul_template::params::init_style,
) inherits consul_template::params {

  validate_bool($purge_config_dir)
  validate_bool($manage_user)

  class { 'consul_template::install': } ->
  class { 'consul_template::config':
    consul_host  => $consul_host,
    consul_port  => $consul_port,
    consul_token => $consul_token,
    consul_retry => $consul_retry,
    purge        => $purge_config_dir,
  } ~>
  class { 'consul_template::run_service': } ->
  Class['consul_template']

}
